;
; C: DrawLine(int x1,int y1,int x2,int y2,char col)
;
        global _DrawLine

; externals

	xref DrawSprite

		text	

x1      = ptr1
y1      = ptr2
x2      = ptr3
y2      = tmp1


_DrawLine:
                lda (sp)
                sta line_scb2_color
                ldy #2
                lda (sp),y
                sta y2
                iny
                lda (sp),y
                sta y2+1
                iny
                lda (sp),y
                sta x2
                iny
                lda (sp),y
                sta x2+1
                iny
                lda (sp),y
                sta y1
                iny
                lda (sp),y
                sta y1+1
                iny
                lda (sp),y
                sta x1
                iny
                lda (sp),y
                stx x1+1
                sec
                tya
                adc sp
                sta sp
                bcc _1
                inc sp+1
_1
                stz line_scb2_xsize
                stz line_scb2_ysize
                sec
                lda x2
                sbc x1
                lda x2+1
                sbc x1+1
                bpl DrawLine0
                  lda x1
                  ldx x2
                  sta x2
                  stx x1
                  lda x1+1
                  ldx x2+1
                  sta x2+1
                  stx x1+1
                  lda y1
                  ldx y2
                  sta y2
                  stx y1
                  lda y1+1
                  ldx y2+1
                  sta y2+1
                  stx y1+1
DrawLine0
                lda #%11000000
                sta line_scb2

                sec
                lda y2
                sbc y1
                sta y2
                lda y2+1
                sbc y1+1
                sta y2+1
                bpl DrawLine1
                  sec
                  lda #0
                  sbc y2
                  sta y2
                  lda #0
                  sbc y2+1
                  sta y2+1
                  lda #%11010000
                  sta line_scb2
DrawLine1:
                lda x1
                sta line_scb2_x
                lda x1+1
                sta line_scb2_x+1
                lda y1
                sta line_scb2_y
                lda y1+1
                sta line_scb2_y+1

                lda y2
                ina
                sta line_scb2_ysize+1
                sta $fc56
                stz $fc57

                stz $fc60
                stz $fc61
                sec
                lda x2
                sbc x1
                ina
                sta $fc62
                stz $fc63
DrawLine1a:     lda $fc92
                bmi DrawLine1a                
                lda $fc53
                sta line_scb2_tilt
                lda $fc54
                sta line_scb2_tilt+1
                bne DrawLine2
                  lda #1
                  sta line_scb2_xsize+1
                bra DrawLine4
DrawLine2:      
                 bit line_scb2_tilt
                 bpl DrawLine3
                   ina
DrawLine3:
                 sta line_scb2_xsize+1
DrawLine4:
                ldax #line_scb2
                jmp DrawSprite

***************
line_scb2:         dc.b $d0,$80|$30,$20
                   dc.w 0,dotimage
line_scb2_x:       dc.w 0
line_scb2_y:       dc.w 0
line_scb2_xsize:   dc.w $100
line_scb2_ysize:   dc.w $100
                   dc.w 0
line_scb2_tilt:    dc.w 0
line_scb2_color:   dc.b $e


dotimage        dc.b 2,$11,0
